#version 460
#extension GL_EXT_ray_query : require

#include "Common.glsl"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

void main()
{
	ivec2 pixelIndex = ivec2(gl_GlobalInvocationID.xy);
	if (pixelIndex.x >= outputSize.x || pixelIndex.y >= outputSize.y)
	{
		return;
	}

	vec2 pixelPos = (vec2(pixelIndex) + vec2(0.5)) / vec2(outputSize);

	vec3 rayOrigin = vec3((pixelPos - 0.5) * vec2(2, -2), 0);
	vec3 rayDir    = vec3(0, 0, 1);

	rayQueryEXT rq;
	rayQueryInitializeEXT(rq, TLAS, gl_RayFlagsOpaqueEXT, 0xFFu, rayOrigin, 0.0, rayDir, 1e9);
	while (rayQueryProceedEXT(rq))
	{
	}

	vec3 color = vec3(pixelPos, 0.0) * 0.25;
	if (rayQueryGetIntersectionTypeEXT(rq, true) == gl_RayQueryCommittedIntersectionTriangleEXT)
	{
		vec2 bary = rayQueryGetIntersectionBarycentricsEXT(rq, true);
		color = vec3(1.0 - bary.x - bary.y, bary.x, bary.y);
	}

	imageStore(outputImage, pixelIndex, vec4(color, 1.0));
}
